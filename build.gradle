/*
 * Copyright 2021 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    kotlin_version = '1.5.31'
    desugar = "1.1.5"
    compose_version = "1.0.5"
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io"
      content {
        includeGroup("com.github.pyamsoft.pydroid")
        includeGroup("com.github.pyamsoft")
      }
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  ext {
    minSdk = 23
    maxSdk = 31

    // Us
    pydroid = "24.3.4"
    cachify = "0.0.26"
    highlander = "0.0.21"
    spark = "2.0.1"

    // Jobs
    workmanager = "2.7.1"

    // Leak Canary
    leakCanary = "2.8.1"

    // Logging
    timber = "5.0.1"

    // Room
    room = "2.3.0"

    // Dagger
    dagger = "2.40.5"

    moshi = "1.13.0"
    retrofit = "2.9.0"
    okhttp = "4.9.3"

    // Compose
    composeActivity = "1.4.0"
    accompanist = "0.20.3"
    coil = "1.4.0"

    // AndroidX
    core = "1.7.0"
    constraintLayout = "2.1.2"
    preference = "1.1.1"
    recyclerView = "1.2.1"
    fragment = "1.4.0"
    swipeRefresh = "1.1.0"
    appCompat = "1.4.0"

    // Material Design
    materialDesign = "1.4.0"

    // Recyclerview
    decorator = "1.3.0"
    fastscroll = "1.1.7"
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.0.4'

    // Gradle version checker
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.41.0'

    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    // Spotless
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.1.2"

    //        classpath 'com.google.gms:google-services:4.3.2'
  }
}

plugins {
  // Fix Android build cache
  // https://github.com/gradle/android-cache-fix-gradle-plugin
  id "org.gradle.android.cache-fix" version "2.4.6" apply false
}

subprojects {
  plugins.withType(com.android.build.gradle.api.AndroidBasePlugin) {
    project.apply plugin: "org.gradle.android.cache-fix"
  }
}

allprojects {
  apply plugin: "com.github.ben-manes.versions"
  apply plugin: "com.diffplug.spotless"

  spotless {
    java {
      target "src/**/*.java"

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    kotlin {
      target "src/**/*.kt"
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    groovyGradle {
      target "*.gradle"
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
  }

  // Ignore non-release libraries
  def isNonStable = { version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io"
      content {
        includeGroup("com.github.pyamsoft.pydroid")
        includeGroup("com.github.pyamsoft")
      }
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    options.deprecation = true
  }
}
