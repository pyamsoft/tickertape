/*
 * Copyright 2021 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id("com.android.application")
  id("com.google.devtools.ksp")
}

//noinspection GroovyMissingReturnStatement
android {
  namespace "com.pyamsoft.tickertape"

  compileSdkVersion rootProject.ext.compileSdk

  defaultConfig {
    applicationId "com.pyamsoft.tickertape"
    minSdkVersion rootProject.ext.minSdk
    targetSdkVersion rootProject.ext.targetSdk
    versionCode 1
    versionName "1"

    // For vector drawables
    vectorDrawables.useSupportLibrary = true
    resConfigs 'en'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17

    // Flag to enable support for the new language APIs
    coreLibraryDesugaringEnabled true
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  signingConfigs {
    debug {
      storeFile file("debug.keystore")
      keyAlias "androiddebugkey"
      keyPassword "android"
      storePassword "android"
    }
    release {
      storeFile new File(BUNDLE_STORE_FILE)
      keyAlias BUNDLE_KEY_ALIAS
      keyPassword BUNDLE_KEY_PASSWD
      storePassword BUNDLE_STORE_PASSWD
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
          "proguard-rules.pro"
    }

    debug {
      signingConfig signingConfigs.debug
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
    }
  }

  buildFeatures {
    viewBinding true
    compose true
  }

  composeOptions {
    kotlinCompilerExtensionVersion "$composeCompiler"
  }
  packagingOptions {
    resources {
      pickFirsts += [
        'META-INF/core_release.kotlin_module',
        'META-INF/ui_release.kotlin_module'
      ]
    }
  }

  // Fixes this error message
  // More than one file was found with OS independent path 'META-INF/core_release.kotlin_module'
}

dependencies {
  coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar"
  ksp "com.google.dagger:dagger-compiler:$dagger"

  // Leak Canary
  debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary"
  implementation "com.squareup.leakcanary:plumber-android:$leakCanary"

  debugImplementation "com.github.pyamsoft.pydroid:autopsy:$pydroid"

  implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist"

  implementation project(':alert')
  implementation project(':core')
  implementation project(':db')
  implementation project(':db-room')
  implementation project(':home')
  implementation project(':main')
  implementation project(':notification')
  implementation project(':portfolio')
  implementation project(':quote')
  implementation project(':stocks')
  implementation project(':stocks-remote')
  implementation project(':ui')
  implementation project(':worker')
  implementation project(':worker-workmanager')
}